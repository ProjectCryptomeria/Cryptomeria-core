# justfile for raidchain project

# --- å¤‰æ•°å®šç¾© ---
RUN_SCRIPT        := "./scripts/make/run.sh"
DEV_IMAGE         := "raidchain/dev-tools:latest"
IGNITE_IMAGE      := "ignitehq/cli:latest"
HELM_RELEASE_NAME := "raidchain"
NAMESPACE         := "raidchain"
IMAGE_DATACHAIN   := "raidchain/datachain:latest"
IMAGE_METACHAIN   := "raidchain/metachain:latest"
IMAGE_RELAYER     := "raidchain/relayer:latest"
DEFAULT_CHAINS    := "7"

# justã‚³ãƒãƒ³ãƒ‰ã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®æŒ™å‹•ã‚’è¨­å®šã€‚ã‚³ãƒãƒ³ãƒ‰ä¸€è¦§ã‚’è¡¨ç¤ºã™ã‚‹ã€‚
default:
    @just --list

# --- Setup Tasks ---

# é–‹ç™ºç”¨ã®å®Ÿè¡Œç’°å¢ƒ(dev-tools)ã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’ãƒ“ãƒ«ãƒ‰
init-runtime:
    #!/usr/bin/env sh
    DOCKER_GID=$(getent group docker | cut -d: -f3)
    docker build --build-arg DOCKER_GID=${DOCKER_GID} -t {{DEV_IMAGE}} -f develop.Dockerfile .

# --- Workflow ---

# [ä¸€æ‹¬å®Ÿè¡Œ] ã‚¯ãƒªãƒ¼ãƒ³ã‚¢ãƒƒãƒ—ã€å†ç”Ÿæˆã€ãƒ“ãƒ«ãƒ‰ã€ãƒ‡ãƒ—ãƒ­ã‚¤ã‚’å…¨ã¦å®Ÿè¡Œ
all-in-one chains=DEFAULT_CHAINS:
    @just clean-all
    @just scaffold-chain
    @just build
    @just deploy {{chains}}
    @echo "âœ… All-in-one process complete!"

# --- Build Tasks ---

# [æ¨å¥¨] å…¨ã¦ã®ã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆã‚’ãƒ“ãƒ«ãƒ‰
build: build-datachain build-metachain build-relayer
    @echo "âœ… All images built."

# datachainã®Dockerã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’ãƒ“ãƒ«ãƒ‰
build-datachain:
    @{{RUN_SCRIPT}} ignite chain build --path ./chain/datachain -o dist --skip-proto
    @{{RUN_SCRIPT}} docker build -t {{IMAGE_DATACHAIN}} -f build/datachain/Dockerfile .

# metachainã®Dockerã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’ãƒ“ãƒ«ãƒ‰
build-metachain:
    @{{RUN_SCRIPT}} ignite chain build --path ./chain/metachain -o dist --skip-proto
    @{{RUN_SCRIPT}} docker build -t {{IMAGE_METACHAIN}} -f build/metachain/Dockerfile .

# relayerã®Dockerã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’ãƒ“ãƒ«ãƒ‰
build-relayer:
    @{{RUN_SCRIPT}} docker build -t {{IMAGE_RELAYER}} -f build/relayer/Dockerfile .

# --- Kubernetes Tasks ---

# Helmã‚’ä½¿ã„ã€Kubernetesã‚¯ãƒ©ã‚¹ã‚¿ã«ãƒ‡ãƒ—ãƒ­ã‚¤ (datachainã®æ•°ã‚’æŒ‡å®šå¯èƒ½)
# ä¾‹: just deploy 4
deploy chains=DEFAULT_CHAINS:
    #!/usr/bin/env sh
    set -e
    echo "--> ğŸš€ Deploying with {{chains}} datachain(s)..."
    TEMP_VALUES_FILE=".helm-temp-values.yaml"
    trap 'rm -f -- "$TEMP_VALUES_FILE"' EXIT
    ./scripts/helm/generate-values.sh {{chains}} > "$TEMP_VALUES_FILE"
    {{RUN_SCRIPT}} helm dependency update k8s/helm/raidchain
    {{RUN_SCRIPT}} helm install {{HELM_RELEASE_NAME}} k8s/helm/raidchain \
        --namespace {{NAMESPACE}} \
        --create-namespace \
        -f "$TEMP_VALUES_FILE"

# ãƒ‡ãƒ—ãƒ­ã‚¤ã•ã‚ŒãŸã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã¨é–¢é€£PVCã‚’ã‚¯ãƒ©ã‚¹ã‚¿ã‹ã‚‰ã‚¢ãƒ³ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«
undeploy:
    @-{{RUN_SCRIPT}} helm uninstall {{HELM_RELEASE_NAME}} --namespace {{NAMESPACE}}
    @echo "--> ğŸ—‘ï¸ Deleting Persistent Volume Claims..."
    @-{{RUN_SCRIPT}} kubectl -n {{NAMESPACE}} delete pvc -l app.kubernetes.io/name={{HELM_RELEASE_NAME}}

# K8sãƒªã‚½ãƒ¼ã‚¹ã‚’ã‚¯ãƒªãƒ¼ãƒ³ã‚¢ãƒƒãƒ—ã—ã¦ã‹ã‚‰ãƒ‡ãƒ—ãƒ­ã‚¤ (datachainã®æ•°ã‚’æŒ‡å®šå¯èƒ½)
# ä¾‹: just deploy-clean 4
deploy-clean chains=DEFAULT_CHAINS:
    @just clean-k8s
    @just deploy {{chains}}
    @echo "âœ… Redeployment complete!"

upgrade:
    @{{RUN_SCRIPT}} helm upgrade {{HELM_RELEASE_NAME}} k8s/helm/raidchain --namespace {{NAMESPACE}} --reuse-values

# --- Logging and Exec ---

# [ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆ] datachainã®ãƒ­ã‚°ã‚’è¡¨ç¤º
logs: logs-datachain

# å…¨ã¦ã®ã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆã®ãƒ­ã‚°ã‚’è¡¨ç¤º
logs-all:
    @{{RUN_SCRIPT}} kubectl logs -f -l app.kubernetes.io/instance={{HELM_RELEASE_NAME}} -n {{NAMESPACE}} --max-log-requests=10

# datachain Podã®ãƒ­ã‚°ã‚’è¡¨ç¤º
logs-datachain:
    @{{RUN_SCRIPT}} kubectl logs -f -l app.kubernetes.io/instance={{HELM_RELEASE_NAME}},app.kubernetes.io/name=datachain -n {{NAMESPACE}}

# metachain Podã®ãƒ­ã‚°ã‚’è¡¨ç¤º
logs-metachain:
    @{{RUN_SCRIPT}} kubectl logs -f -l app.kubernetes.io/instance={{HELM_RELEASE_NAME}},app.kubernetes.io/name=metachain -n {{NAMESPACE}}

# --- Development Tasks ---

# ãƒã‚§ãƒ¼ãƒ³ã®å‹•ä½œç¢ºèªãƒ†ã‚¹ãƒˆã‚’å®Ÿè¡Œ
test:
    @{{RUN_SCRIPT}} ./scripts/test/chain-integrity-test.sh

# æ–°ã—ã„ãƒã‚§ãƒ¼ãƒ³ã®ã²ãªå½¢ã‚’ç”Ÿæˆ
scaffold-chain:
    @just scaffold-datachain
    @just scaffold-metachain
    @echo "âœ… Scaffold complete! Check the 'chain' directory."

scaffold-datachain:
    @{{RUN_SCRIPT}} ./scripts/scaffold/scaffold-chain.sh datachain datastore

scaffold-metachain:
    @{{RUN_SCRIPT}} ./scripts/scaffold/scaffold-chain.sh metachain metastore

# --- Cleanup Tasks ---

# K8sãƒªã‚½ãƒ¼ã‚¹ã‚’å‰Šé™¤ã—ã€ç”Ÿæˆã•ã‚ŒãŸãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚‚å‰Šé™¤
clean-all: clean-k8s clean-chain
    @echo "âœ… Full cleanup complete!"

clean-chain:
    @echo "--> ğŸ—‘ï¸ Deleting generated chain directories from host..."
    @rm -rf chain/datachain chain/metachain

# K8sãƒªã‚½ãƒ¼ã‚¹(Namespaceã”ã¨)ã‚’å‰Šé™¤
clean-k8s: undeploy
    @echo "--> ğŸ—‘ï¸ Deleting namespace {{NAMESPACE}}..."
    @{{RUN_SCRIPT}} kubectl delete namespace {{NAMESPACE}} --ignore-not-found

# --- Controller Tasks ---
# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼] ä¾å­˜ãƒ‘ãƒƒã‚±ãƒ¼ã‚¸ã‚’ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«
ctl-install:
    @{{RUN_SCRIPT}} bash -c "cd controller && yarn install"

# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼] é–‹ç™ºã‚µãƒ¼ãƒãƒ¼ã‚’èµ·å‹•
ctl-dev:
    @{{RUN_SCRIPT}} bash -c "cd controller && yarn start"

# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼] ã‚³ãƒãƒ³ãƒ‰ã‚’å®Ÿè¡Œ (æ±ç”¨)
ctl-exec *args:
    @{{RUN_SCRIPT}} bash -c "cd controller && yarn {{args}}"

# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼] testsãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå†…ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’å®Ÿè¡Œ
ctl-test *args:
    @{{RUN_SCRIPT}} bash -c "cd controller && yarn ts-node src/tests/{{args}}"

# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼] æŒ‡å®šã•ã‚ŒãŸãƒ†ã‚¹ãƒˆã‚±ãƒ¼ã‚¹ã‚’å®Ÿè¡Œ (ä¾‹: just ctl-test --case 1)
ctl-test-case *args:
    @{{RUN_SCRIPT}} bash -c "cd controller && yarn test {{args}}"

# --- Runtime Tasks ---
# ãƒ©ãƒ³ã‚¿ã‚¤ãƒ ç”¨ã‚³ãƒ³ãƒ†ãƒŠã«å…¥ã‚‹
runtime-shell:
    @{{RUN_SCRIPT}} bash

# ã‚³ãƒ³ãƒ†ãƒŠå†…ã§ã‚³ãƒãƒ³ãƒ‰ã‚’å®Ÿè¡Œã™ã‚‹ãŸã‚ã®ãƒ©ãƒƒãƒ‘ãƒ¼
run *args:
    @{{RUN_SCRIPT}} {{args}}

# [ã‚³ãƒ³ãƒˆãƒ­ãƒ¼ãƒ©ãƒ¼]ã‚³ãƒãƒ³ãƒ‰ã‚’å®Ÿè¡Œã™ã‚‹ãŸã‚ã®ãƒ©ãƒƒãƒ‘ãƒ¼
run-ctl *args:
    @{{RUN_SCRIPT}} bash -c "cd controller && {{args}}"